plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.4.18"

    id "org.sonarqube" version "2.8"

    id 'maven'
    id "com.github.ben-manes.versions" version "0.28.0"
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

javadoc {
    options.tags = ["date"]
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def myPlugins = []

if (idea_version.startsWith('193.') || idea_version.startsWith('2019.3') || idea_version.startsWith('IU-201.') || idea_version.startsWith('201.')) {
    myPlugins = ['java']
}

sonarqube {
    properties {
        property "sonar.projectKey", "p3c-idea"
        property "sonar.organization", "xenoamess-github"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "023904832cfec46a1aa61bb7aeee156b066cb3bc"
    }
}

intellij {
    version idea_version
    plugins = myPlugins
    pluginName plugin_name
    updateSinceUntilBuild false
    sandboxDirectory "$project.buildDir/idea-sandbox/$idea_version"
}
version plugin_version

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

dependencies {
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
    compile 'com.xenoamess.p3c:p3c-pmd:2.0.2.5x-SNAPSHOT'
    compile group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getProperty('ossrhUsername'), password: System.getProperty('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getProperty('ossrhUsername'), password: System.getProperty('ossrhPassword'))
            }

            pom.project {
                name 'p3c-common'
                packaging 'jar'
                description 'P3c Idea Plugin Common.'
                url 'https://github.com/xenoamess/p3c'

                scm {
                    url 'https://github.com/xenoamess/p3c'
                    connection 'scm:git:https://git@github.com/xenoamess/p3c.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'junlie'
                        name 'Junlie'
                        email 'sean.caikang@gmail.com'
                    }
                    developer {
                        id 'ZengHou'
                        name 'ZengHou'
                        email 'fengwei1983@gmail.com'
                    }
                    developer {
                        id 'XenoAmess'
                        name 'XenoAmess'
                        email 'xenoamess@gmail.com'
                        organization 'XenoAmess'
                        url 'https://github.com/xenoamess'
                    }
                }
            }
        }
    }
}

signing {
    required {
        isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}
