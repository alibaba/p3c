plugins {
    id "org.jetbrains.intellij" version "1.13.1"
}
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

javadoc {
    options.tags = ["date"]
}

java {
    withJavadocJar()
    withSourcesJar()
}

def myPlugins = ['git4idea']
def versionDotIndex = idea_version.indexOf('.')
def intVersion = versionDotIndex == -1 ? Integer.parseInt(idea_version) : Integer.parseInt(idea_version.substring(0, versionDotIndex))
if (intVersion >= 2023) {
    myPlugins = ['vcs-git', 'java']
} else if (intVersion >= 2019) {
    myPlugins = ['git4idea', 'java']
}

intellij {
    version.set(idea_version)
    plugins.set(myPlugins)
    pluginName.set(plugin_name)
    updateSinceUntilBuild.set(false)
    sandboxDir.set("$project.buildDir/idea-sandbox/$idea_version")
}
version '2.0.1'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

dependencies {
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.25-incubating'
    implementation 'com.alibaba.p3c:p3c-pmd:2.1.1'
    implementation group: 'org.javassist', name: 'javassist', version: '3.21.0-GA'
}

publishing {
    repositories {
        maven {
            if (version.toString().toUpperCase().contains("SNAPSHOT")) {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            } else {
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
            }
            credentials {
                username = "${property("ossrhUsername")}"
                password = "${property("ossrhPassword")}"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'p3c-common'
            from components.java
            pom {
                name = 'p3c-common'
                description = 'P3c Idea Plugin Common.'
                url = 'https://github.com/alibaba/p3c'

                scm {
                    url = 'https://github.com/alibaba/p3c'
                    connection = 'scm:git:https://git@github.com/alibaba/p3c.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'junlie'
                        name = 'Junlie'
                        email = 'sean.caikang@gmail.com'
                    }
                    developer {
                        id = 'ZengHou'
                        name = 'ZengHou'
                        email = 'fengwei1983@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
