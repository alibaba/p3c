plugins {
    id "org.jetbrains.intellij" version "1.13.1"
}
apply plugin: 'kotlin'
apply plugin: 'idea'

def myPlugins = ['git4idea']
def versionDotIndex = idea_version.indexOf('.')
def intVersion = versionDotIndex == -1 ? Integer.parseInt(idea_version) : Integer.parseInt(idea_version.substring(0, versionDotIndex))
if (intVersion >= 2023) {
    myPlugins = ['vcs-git', 'java']
} else if (intVersion >= 2019) {
    myPlugins = ['git4idea', 'java']
}

intellij {
    version.set(idea_version)
    plugins.set(myPlugins)
    pluginName.set(plugin_name)
    updateSinceUntilBuild.set(false)
    sandboxDir.set("idea-sandbox")
}

tasks {

    patchPluginXml {
        sinceBuild.set(pluginSinceBuild)
        untilBuild.set(pluginUntilBuild)
        pluginId.set("io.github.godfather1103.alibaba.p3c")
        pluginDescription.set("""
<h2>English Readme：</h2>
<p>Alibaba Java Coding Guidelines plugin support.Fix some bug.such as <a href="https://github.com/alibaba/p3c/issues/898">issues-898</a>,<a href="https://github.com/alibaba/p3c/issues/900">issues-900</a></p>
<p>Official plugin URL: <a href="https://plugins.jetbrains.com/plugin/10046-alibaba-java-coding-guidelines">Alibaba Java Coding Guidelines</a></p>

<h2>中文说明：</h2>
<p>阿里巴巴Java编码规范检查插件。修复了一些官方一直未修复的Bug。如 <a href="https://github.com/alibaba/p3c/issues/898">issues-898</a>,<a href="https://github.com/alibaba/p3c/issues/900">issues-900</a></p>
<p>官方插件地址：<a href="https://plugins.jetbrains.com/plugin/10046-alibaba-java-coding-guidelines">Alibaba Java Coding Guidelines</a></p>

<h2>捐赠(Donate)</h2>
<pre>
你的馈赠将助力我更好的去贡献，谢谢！
Your gift will help me to contribute better, thank you!

<a href="https://paypal.me/godfather1103?locale.x=zh_XC">PayPal</a>
        """)
        changeNotes.set("""
        <ul>
        2.1.1
        <li>fix(<a href="https://github.com/alibaba/p3c/issues/898">issues-898</a>): 1、修复2022.1中检测到缺失override注解出现异常；2、修复编译异常</li>
        <li>fix(<a href="https://github.com/alibaba/p3c/issues/900">issues-900</a>): 修复驼峰等自动修改异常</li>
        </ul>
        """)
    }

    publishPlugin {
        token.set(publishToken.isEmpty() ? ORG_GRADLE_PROJECT_intellijPublishToken : publishToken)
    }
}

version plugin_version

dependencies {
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.25-incubating'
    implementation project(':p3c-common')
    implementation group: 'org.javassist', name: 'javassist', version: '3.21.0-GA'
}

